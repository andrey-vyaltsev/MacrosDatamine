!START CNTRSURF

!ONERR GOTO ERR 

!SCROFF 

-- Макрос построения центральных поверхностей
-- Версия от 17/01/2024
-- Автор - Кирьяков Г.А. g-kir@yandex.ru

==================================================
Блок настроек
==================================================

-- Исходный каркас (входные данные)
!LET $tr_in# = wf_ore_
!LET $pt_in# = wf_ore_

-- Поле зонального контроля (входные данные). Если нет - оставить переменную пустой
!LET $zone_fld# = BODY

-- Тип поля зонального контроля: 1 - цифровой, 2 - алфавитный (входные данные)
!LET $zone_type# = 0

-- Поля среднего залегания каркаса (входные данные)
!LET $trdipdir_av# = DIPDIR
!LET $trdip_av# = DIP

-- Итоговый каркас (будет создан макросом)
!LET $tr_ou# = cntr_surf0_tr
!LET $pt_ou# = cntr_surf0_pt

-- Размер треугольников итогового каркаса
!LET $tr_size# = 5

============================================================================
Блок настроек закончен. Ниже ничего не трогать без согласования с автором
============================================================================

!LET $DEBUG# = 0
!LET $maxlink# = $tr_size# * 5
!LET $debug_step# = 0

Алгоритм:
1. Проверяем задано ли поле зонального контроля:
    а. если поле задано - формируем список значений (zone_list_mac)
    б. если поле не задано - формируем фиктивное поле зонального контроля (пусть будет BQKLMR = 1)
       и фиктивный список из одной записи

1.5. Если поле зонального контроля алфавитное - определяем его длину

2. Переносим поле зонального контроля в точки (если поле зонального контроля не задано - просто создаём фиктивное сразу в точках)
3. Перебираем в цикле все значения поля зонального контроля. Каждую итерацию:
    а. выделяем треугольники и точки, относящиеся к данному значению
    б. поворачиваем точки каркаса до горизонта
    в. формируем прототип с вертикальными ячейками
    г. заполняем повёрнутый каркас без дробления по горизонту
    д. строим DTM по центрам ячеек
    е. заносим туда значение зонального контроля
    ж. поворачиваем точки взад
    з. формируем финальный файл поверхностей

Поехали:

1. Проверяем задано ли поле зонального контроля:

!LET $zone_l# = LENG($zone_fld#)

!IF $zone_l# > 0,THEN GOTO 
    а. если поле задано - формируем список значений (zone_list_mac)

 !SORTX    &IN($tr_in#),
           &OUT(tmp_mac_tr),
           *KEY1($zone_fd#),
           @BINS=5.0,
           @ORDER=1.0

 !SELCOP   &IN(tmp_mac_tr),
           &OUT(zone_list_mac),
           *F1($zone_fd#),
           @KEEPALL=0.0

 !IF $DEBUG# = 0,THEN 
  
  !DELETE   &IN(tmp_mac_tr),
            @CONFIRM=0.0

 !ENDIF 
 
 ----Проверка 1. Начало.----
 !IF $DEBUG# = 1,THEN 
 !PROMPT 
 0 Проверка 1
 1 Сколько всего значений содержится в файле zone_list_mac>'$t#'
 !FILE $file_name#=zone_list_mac,$test_true#=recs
 !IF $t# <> $test_true#,GOTO ERR
 !ECHO Проверка 1 пройдена успешно!
 !ENDIF 
 ----Проверка 1. Конец.----

 !LET $debug_step# = 1

1.5. Если поле зонального контроля алфавитное - определяем его длину

 !IF $zone_type# = 2,THEN 

   !EXTRA    &IN(zone_list_mac),
             &OUT(tmp_mac0),
             @APPROX=0.0
   
    LNGZN = len('$zone_fld#')
   
   GO 

   !STATS    &IN(tmp_mac0),
             &OUT(stat_mac_tmp),
             *F1(LNGZN),
             @KEYSORT=1.0,
             @KEYTOL=0.00001,
             @PCNTILES=1.0,
             @SORTOUT=1.0

   !FIELD $file_name#=stat_mac_tmp,
          $record_num#=1,
          $zone_len#=MAXIMUM

   !LET $zone_len# = {INT($zone_len# / 4) * 4 + 4}

   !IF $DEBUG# = 0,THEN 

    !DELETE   &IN(tmp_mac0),
              @CONFIRM=0.0

    !DELETE   &IN(stat_mac_tmp),
              @CONFIRM=0.0

   !ENDIF 

  !LET $debug_step# = 2

 !ENDIF 

 2. Переносим поле зонального контроля в точки

 !LET $stzn_tr_in# = $tr_in#
 !LET $stzn_pt_in# = $pt_in#
 !LET $stzn_zone_fld# = $zone_fld#
 !LET $stzn_pt_ou# = wf_wrk_pt

 !GOSUB STZN

 ----Проверка 4. Начало.----
 !IF $DEBUG# = 1,THEN 
 !PROMPT 
 0 Проверка 4
 1 Сколько всего записей в файле $stzn_pt_ou#>'$t#'
 !FILE $file_name#=$stzn_pt_ou#,$test_true#=recs
 !IF $t# <> $test_true#,GOTO ERR
 !ECHO Проверка 4 пройдена успешно!
 !ENDIF 
 ----Проверка 4. Конец.----

 !LET $debug_step# = 3

 !COPY     &IN($tr_in#),
           &OUT(wf_wrk_tr)

 !LET $debug_step# = 4

!ELSE 

    б. если поле не задано - формируем фиктивное поле зонального контроля (пусть будет BQKLMR = 1)
       и фиктивный список из одной записи

 !LET $file_name_mf# = zone_list_mac
 !LET $fld_name_mf# = BQKLMR
 !LET $zone_type# = 1
 !LET $fld_type_mf# = 'N'
 !LET $fld_dflt_vl_mf# = 1

 !GOSUB STMKFILE

 !LET $debug_step# = 5

 если поле зонального контроля не задано - просто создаём фиктивное сразу в точках

 !EXTRA    &IN($pt_in#),
           &OUT(wf_wrk_pt),
           @APPROX=0.0

  BQKLMR == 1

 GO 

 !EXTRA    &IN($tr_in#),
           &OUT(wf_wrk_tr),
           @APPROX=0.0

  BQKLMR == 1

 GO 

 !LET $debug_step# = 6

!ENDIF 

К этому моменту есть:

- рабочая версия каркаса: wf_wrk_tr и wf_wrk_pt - оба с полем зонального контроля
- список значений зонального контроля: zone_list_mac

----Проверка 5. Начало.----
!IF $DEBUG# = 1,THEN 
!PROMPT 
0 Проверка 5
1 Сколько всего записей в файле wf_wrk_tr>'$t#'
!FILE $file_name#=wf_wrk_tr,$test_true#=recs
!IF $t# <> $test_true#,GOTO ERR
!ECHO Проверка 5 пройдена успешно!
!ENDIF 
----Проверка 5. Конец.----

3. Перебираем в цикле все значения поля зонального контроля. 

!ECHO Сформированы рабочие копии каркасов и список значений зонального контроля
!ECHO Начинаю перебор

!FILE $file_name#=zone_list_mac,
      $times#=recs

!LET $counter# = 1

----Проверка 6. Начало.----
!IF $DEBUG# = 1,THEN 
!PROMPT 
0 Проверка 6
1 Чему на данный момент равно значение переменной counter>'$t#'
!IF $t# <> $counter#,GOTO ERR
!ECHO Проверка 6 пройдена успешно!
!ENDIF 
----Проверка 6. Конец.----

!LOOPST:REM 

 Каждую итерацию:
 читаем текущее значение поля зонального контроля из списка

 !FIELD $file_name#=zone_list_mac,
        $record_num#=$counter#,
        $curr_zone#=$zone_fld#
  
 а. выделяем треугольники и точки, относящиеся к данному значению
  
 !IF $zone_type# = 1,THEN 

  !COPY     &IN(wf_wrk_tr),
            &OUT(wf_wrk_tr1),
            $zone_fld#=$curr_zone#

  !COPY     &IN(wf_wrk_pt),
            &OUT(wf_wrk_pt1),
            $zone_fld#=$curr_zone#

 !ELSE 

  !COPY     &IN(wf_wrk_tr),
            &OUT(wf_wrk_tr1),
            $zone_fld#='$curr_zone#'

  !COPY     &IN(wf_wrk_pt),
            &OUT(wf_wrk_pt1),
            $zone_fld#='$curr_zone#'

 !ENDIF 

 !LET $debug_step# = 7

 б. поворачиваем точки каркаса до горизонта

 !FIELD $file_name#=wf_wrk_tr1,
        $record_num#=1,
        $curr_az#=$trdipdir_av#,
        $curr_ug#=$trdip_av#

 !CDTRAN   &IN(wf_wrk_pt1),
           &OUT(wf_wrk_pt2),
           *X(X),
           *Y(Y),
           *Z(Z),
           *NEWX(XP),
           *NEWY(YP),
           *NEWZ(ZP),
           @ANGLE1=$curr_az#,
           @ANGLE2=$curr_ug#,
           @ANGLE3=0,
           @ROTAXIS1=3.0,
           @ROTAXIS2=1.0,
           @ROTAXIS3=3.0,
           @X0=0.0,
           @Y0=0.0,
           @Z0=0.0,
           @XR0=0.0,
           @YR0=0.0,
           @ZR0=0.0,
           @FACTOR=1.0,
           @INVERSE=0.0

 !LET $debug_step# = 8

 !IF $DEBUG# = 0,THEN 

  !DELETE   &IN(wf_wrk_pt1),
            @CONFIRM=0.0

 !ENDIF 

 ----Проверка 7. Начало.----
 !IF $DEBUG# = 1,THEN 
 !IF $counter# = 1,THEN 
 !PROMPT 
 0 Проверка 7
 1 Сколько всего записей в файле wf_wrk_pt1>'$t#'
 !FILE $file_name#=wf_wrk_pt1,$test_true#=recs
 !IF $t# <> $test_true#,GOTO ERR
 !ECHO Проверка 7 пройдена успешно!
 !ENDIF 
 !ENDIF 
 ----Проверка 7. Конец.----

 в. формируем прототип с вертикальными ячейками (curr_proto_mac)

 !LET $crdIn# = wf_wrk_pt2
 !LET $x_crd# = XP
 !LET $y_crd# = YP
 !LET $z_crd# = ZP
 !LET $x_size# = $tr_size#
 !LET $y_size# = $tr_size#
 !LET $z_size# = 1000
 !LET $x_margin# = $tr_size#
 !LET $y_margin# = $tr_size#
 !LET $z_margin# = $tr_size#
 !LET $protoOu# = curr_proto_mac

 !GOSUB STARTPROTO

 ----Проверка 8. Начало.----
 !IF $DEBUG# = 1,THEN 
 !IF $counter# = 1,THEN 
 !PROMPT 
 0 Проверка 8
 1 Какая минимальная координата по оси X у прототипа $protoOu#>'$t#'
 !FIELD $file_name#=$protoOu#,$record_num#=0,$test_true#=XMORIG
 !IF $t# <> $test_true#,GOTO ERR
 !ECHO Проверка 8 пройдена успешно!
 !ENDIF 
 !ENDIF 
 ----Проверка 8. Конец.----

 !LET $debug_step# = 9

 г. заполняем повёрнутый каркас без дробления по горизонту

 !TRIFIL   &PROTO($protoOu#),
           &WIRETR(wf_wrk_tr1),
           &WIREPT(wf_wrk_pt2),
           &МОДЕЛЬ(curr_mod_mac),
           @MODLTYPE=1.0,
           @MAXDIP=0.0,
           @SPLITS=0.0,
           @PLANE='XY  ',
           @XSUBCELL=1.0,
           @YSUBCELL=1.0,
           @ZSUBCELL=1.0,
           @RESOL=0.0,
           @CHECKROT=0.0,
           @AUTOSORT=1.0

 !IF $DEBUG# = 0,THEN 

  !DELETE   &IN(wf_wrk_tr1),
            @CONFIRM=0.0

  !DELETE   &IN(wf_wrk_pt2),
            @CONFIRM=0.0

  !DELETE   &IN(curr_proto_mac),
            @CONFIRM=0.0

 !ENDIF 

 !LET $debug_step# = 10

 д. строим DTM по центрам ячеек

 !SURTRI   &POINTIN(curr_mod_mac),
           &WIRETR(curr_cntr_tr),
           &WIREPT(curr_cntr_pt),
           *XPT(XC),
           *YPT(YC),
           *ZPT(ZC),
           @SURFACE=1.0,
           @BOUNDARY=0.0,
           @SYSTEM=3.0,
           @MAXLINK=$maxlink#,
           @COG=0.0,
           @ERRTRACE=0.0,
           @HBRATIO=0.05,
           @MAXPTS=-,
           @CHECK=1.0

 !IF $DEBUG# = 0,THEN 

  !DELETE   &IN(curr_mod_mac),
            @CONFIRM=0.0

 !ENDIF 

 ----Проверка 9. Начало.----
 !IF $DEBUG# = 1,THEN 
 !IF $counter# = 1,THEN 
 !PROMPT 
 0 Проверка 9
 1 Какое значение прописано в строке 1 поля SID в файле curr_cntr_tr>'$t#'
 !FIELD $file_name#=curr_cntr_tr,$record_num#=1,$test_true#=SID
 !IF $t# <> $test_true#,GOTO ERR
 !ECHO Проверка 9 пройдена успешно!
 !ENDIF 
 !ENDIF 
 ----Проверка 9. Конец.----

 !LET $debug_step# = 11

 е. заносим туда значение зонального контроля

 !IF $zone_type# = 1,THEN 

  !EXTRA    &IN(curr_cntr_tr),
            &OUT(curr_cntr_tr1),
            @APPROX=0.0

  '$zone_fld#' = $curr_zone#

  CS:GO  

 !ELSE 

  !EXTRA    &IN(curr_cntr_tr),
            &OUT(curr_cntr_tr1),
            @APPROX=0.0

  '$zone_fld#;A$zone_len#' = "$curr_zone#"

  GO 

 !ENDIF 

 !IF $DEBUG# = 0,THEN 

  !DELETE   &IN(curr_cntr_tr),
            @CONFIRM=0.0

 !ENDIF 

 ----Проверка 10. Начало.----
 !IF $DEBUG# = 1,THEN 
 !IF $counter# = 1,THEN 
 !PROMPT 
 0 Проверка 10
 1 Какое значение прописано в строке 1 поля BODY в файле curr_cntr_tr1>'$t#'
 !FIELD $file_name#=curr_cntr_tr1,$record_num#=1,$test_true#=BODY
 !IF $t# <> $test_true#,GOTO ERR
 !ECHO Проверка 10 пройдена успешно!
 !ENDIF 
 !ENDIF 
 ----Проверка 10. Конец.----

 !LET $debug_step# = 12

 ж. поворачиваем точки взад

 !CDTRAN   &IN(curr_cntr_pt),
           &OUT(curr_cntr_pt1),
           *X(XP),
           *Y(YP),
           *Z(ZP),
           *NEWX(XP),
           *NEWY(YP),
           *NEWZ(ZP),
           @ANGLE1=$curr_az#,
           @ANGLE2=$curr_ug#,
           @ANGLE3=0,
           @ROTAXIS1=3.0,
           @ROTAXIS2=1.0,
           @ROTAXIS3=3.0,
           @X0=0.0,
           @Y0=0.0,
           @Z0=0.0,
           @XR0=0.0,
           @YR0=0.0,
           @ZR0=0.0,
           @FACTOR=1.0,
           @INVERSE=1.0

 !IF $DEBUG# = 0,THEN 

  !DELETE   &IN(curr_cntr_pt),
            @CONFIRM=0.0

 !ENDIF 

 !LET $debug_step# = 14

 з. формируем финальный файл поверхностей

 !FILE $file_name#=$tr_ou#,$rec_count#=recs

 !IF $rec_count# = 0,THEN 

  !COPY     &IN(curr_cntr_tr1),
            &OUT($tr_ou#)

  !COPY     &IN(curr_cntr_pt1),
            &OUT($pt_ou#)

 !ELSE 

  !ADDTRI   &WIRETR1($tr_ou#),
            &WIREPT1($pt_ou#),
            &WIRETR2(curr_cntr_tr1),
            &WIREPT2(curr_cntr_pt1),
            &WIRETROU($tr_ou#_),
            &WIREPTOU($pt_ou#_)

  !SORTX    &IN($tr_ou#_),
            &OUT($tr_ou#),
            *KEY1($zone_fld#),
            @BINS=5.0,
            @ORDER=1.0 

  !LET $debug_step# = 15

  !COPY     &IN($pt_ou#_),&OUT($pt_ou#)

  !IF $DEBUG# = 0,THEN 

   !DELETE   &IN($tr_ou#_),
             @CONFIRM=0.0

   !DELETE   &IN($pt_ou#_),
             @CONFIRM=0.0

  !ENDIF 

 !ENDIF 

 !IF $DEBUG# = 0,THEN 

  !DELETE   &IN(curr_cntr_tr1),
            @CONFIRM=0.0

  !DELETE   &IN(curr_cntr_pt1),
            @CONFIRM=0.0

 !ENDIF 

 !ECHO Обработано тело $curr_zone# ($counter# из $times#)

 !LET $counter# = {INT($counter# + 1)}

!WHEN $counter# <= $times#,GOTO LOOPST

!IF $DEBUG# = 0,THEN 

 !DELETE   &IN(wf_wrk_tr),
           @CONFIRM=0.0

 !DELETE   &IN(wf_wrk_pt),
           @CONFIRM=0.0

!ENDIF 

==================================================
Раздел подпрограмм
==================================================

===
Создаём однострочный файл с полем, определённым пользователем
===

!GOTO ENDMKFILE

!STMKFILE:REM 

$file_name_mf# - имя выходного файла
$fld_name_mf# - имя поля
$fld_type_mf# (A/N) - тип поля
$fld_len_mf# - длина поля для алфавитного типа
$fld_dflt_vl_mf# - значение поля (в т.ч. по умолчанию)

!PROTOM   &OUT(sub_mf_tmp),
          @ROTMOD=0.0
Y
Y
0
0
0
1
1
1
1
1
1

!PERFIL   &PROTO(sub_mf_tmp),
          &MODEL(sub_mf_tmp1),
          @MODE=0.0,
          @PLANE='XY  ',
          @ZONE=0.0,
          @OPTIMISE=2.0,
          @FULLCELL=0.0,
          @XSUBCELL=1.0,
          @YSUBCELL=1.0,
          @ZSUBCELL=1.0,
          @RESOL=0.0,
          @OVCHECK=1.0,
          @CHECKROT=1.0

!DELETE   &IN(sub_mf_tmp),
          @CONFIRM=0.0


!IF $fld_type_mf# = 'N',THEN 

 !LET $extr_cmd_mf# = ''$fld_name_mf#' = $fld_dflt_vl_mf#'

!ELSE 

 !LET $extr_cmd_mf# = ''$fld_name_mf#;A$fld_len_mf#' = "$fld_dflt_vl_mf#"'

!ENDIF 


!EXTRA    &IN(sub_mf_tmp1),
          &OUT($file_name_mf#),
          @APPROX=0.0

 $extr_cmd_mf#
 saveonly($fld_name_mf#)

GO 

!DELETE   &IN(sub_mf_tmp1),
          @CONFIRM=0.0


!DMEDIT   &IN($file_name_mf#)
V
$fld_name_mf#
$fld_dflt_vl_mf#
E

!RETURN 

!ENDMKFILE:REM 

===
Подпрограмма переноса зонального контроля из треугольников в точки
===

!GOTO ENDZONE

!STZN:REM 

настройки задаются переменными перед обращением к подпрограмме

Должны быть заданы:

$stzn_tr_in# - входной файл треугольников
$stzn_pt_in# - входной файл точек
$stzn_zone_fld# - поле зонального контроля
$stzn_pt_ou# - выходной файл точек с полем зонального контроля

!LET $pid_num# = 1

!GOSUB STARTSUB1

!LET $pid_num# = 2

!GOSUB STARTSUB1

----Проверка 2. Начало.----
!IF $DEBUG# = 1,THEN 
!PROMPT 
0 Проверка 2
1 Введите значение поля PID в строке 556 файла pid_mac$pid_num#>'$t#'
!FIELD $file_name#=pid_mac$pid_num#,$record_num#=556,$test_true#=PID
!IF $t# <> $test_true#,GOTO ERR
!ECHO Проверка 2 пройдена успешно!
!ENDIF 
----Проверка 2. Конец.----

!LET $pid_num# = 3

!GOSUB STARTSUB1_

----Проверка 3. Начало.----
!IF $DEBUG# = 1,THEN 
!PROMPT 
0 Проверка 3
1 Введите значение поля PID в строке 556 файла pid_mac$pid_num#>'$t#'
!FIELD $file_name#=pid_mac$pid_num#,$record_num#=556,$test_true#=PID
!IF $t# <> $test_true#,GOTO ERR
!ECHO Проверка 3 пройдена успешно!
!ENDIF 
----Проверка 3. Конец.----

!APPEND   &IN1(PID_MAC1),
          &IN2(PID_MAC2),
          &OUT(PID_MAC12),
          @SEQUENCE=0.0,
          @PROTODD=0.0

!DELETE   &IN(PID_MAC1),
          @CONFIRM=0.0

!DELETE   &IN(PID_MAC2),
          @CONFIRM=0.0

!APPEND   &IN1(PID_MAC12)
          &IN2(PID_MAC3),
          &OUT(PID_MAC123),
          @SEQUENCE=0.0,
          @PROTODD=0.0

!DELETE   &IN(PID_MAC12),
          @CONFIRM=0.0

!DELETE   &IN(PID_MAC3),
          @CONFIRM=0.0

!SORTX    &IN(PID_MAC123),
          &OUT(PID_MAC123_1),
          *KEY1(PID),
          *KEY2($stzn_zone_fld#),
          @BINS=5.0,
          @ORDER=1.0

!DELETE   &IN(PID_MAC123),
          @CONFIRM=0.0

!SELCOP   &IN(PID_MAC123_1),
          &OUT(PID_MAC_ALL),
          *F1(PID),
          *F2($stzn_zone_fld#),
          *F3(VDVRLA),
          @KEEPALL=0.0

!DELETE   &IN(PID_MAC123_1),
          @CONFIRM=0.0

!SORTX    &IN($stzn_pt_in#),
          &OUT(pt_mac_tmp),
          *KEY1(PID),
          @BINS=5.0,
          @ORDER=1.0

!JOIN     &IN1(pt_mac_tmp),
          &IN2(PID_MAC_ALL),
          &OUT(PID_MAC_ALL_1),
          *KEY1(DIP),
          @SUBSETR=0.0,
          @SUBSETF=0.0,
          @CARTJOIN=0.0

!DELETE   &IN(pt_mac_tmp),
          @CONFIRM=0.0

!DELETE   &IN(PID_MAC_ALL),
          @CONFIRM=0.0

!EXTRA    &IN(PID_MAC_ALL_1),
          &OUT($stzn_pt_ou#),
          @APPROX=0.0

 if (VDVRLA = absent())
  delete()
 end
 erase (VDVRLA)

GO 

!DELETE   &IN(PID_MAC_ALL_1),
          @CONFIRM=0.0

!RETURN 

!ENDZONE:REM 

===
Подпрограмма сохранения заданного PID из файла точек
===

!GOTO ENDSUB1

!STARTSUB1:REM 

$pid_num# - номер сохраняемого PID

!EXTRA    &IN($stzn_tr_in#),
          &OUT(PID_MAC$pid_num#),
          @APPROX=0.0

 PID = PID$pid_num#
 VDVRLA = 1
 save(PID,$zone_fld#,VDVRLA)

GO 

!RETURN 

!ENDSUB1:REM 

===
Подпрограмма создания прототипа по данным с координатными полями
===

!GOTO ENDPROTO

!STARTPROTO:REM 

$crdIn# - файл данных
$x_crd# - поле координаты X
$y_crd# - поле координаты Y
$z_crd# - поле координаты Z
$x_size# - размер материнской ячейки по X
$y_size# - размер материнской ячейки по Y
$z_size# - размер материнской ячейки по Z
$x_margin# - "добавка" к размаху координат данных по X
$y_margin# - "добавка" к размаху координат данных по Y
$z_margin# - "добавка" к размаху координат данных по Z
$protoOu# - имя файла выходного прототипа

!STATS    &IN($crdIn#),
          &OUT(stat_mac_tmp),
          *F1($x_crd#),
          *F2($y_crd#),
          *F3($z_crd#),
          @KEYSORT=1.0,
          @KEYTOL=0.00001,
          @PCNTILES=1.0,
          @SORTOUT=1.0

!FIELD $file_name#=stat_mac_tmp,
       $record_num#=1,
       $x_min#=MINIMUM,
       $x_max#=MAXIMUM

!FIELD $file_name#=stat_mac_tmp,
       $record_num#=2,
       $y_min#=MINIMUM,
       $y_max#=MAXIMUM

!FIELD $file_name#=stat_mac_tmp,
       $record_num#=3,
       $z_min#=MINIMUM,
       $z_max#=MAXIMUM

!DELETE   &IN(stat_mac_tmp),
          @CONFIRM=0.0

!LET $x_min# = {INT($x_min# / 5) * 5 - $x_size# - $x_margin#}
!LET $y_min# = {INT($y_min# / 5) * 5 - $y_size# - $y_margin#}
!LET $z_min# = INT($z_min# / 5) * 5 - $z_size# - $z_margin#}

!LET $x_max# = {INT($x_max# / 5) * 5 + $x_size# + $x_margin#}
!LET $y_max# = {INT($y_max# / 5) * 5 + $y_size# + $y_margin#}
!LET $z_max# = {INT($z_max# / 5) * 5 + $z_size# + $z_margin#}

!LET $nx# = {INT(($x_max# - $x_min#) / $x_size#) + 1}
!LET $ny# = INT(($y_max# - $y_min#) / $y_size#) + 1}
!LET $nz# = {INT(($z_max# - $z_min#) / $z_size#) + 1}

!PROTOM   &OUT($protoOu#),
          @ROTMOD=0.0
Y
Y
$x_min#
$y_min#
$z_min#
$x_size#
$y_size#
$z_size#
$nx#
$ny#
$nz#

!RETURN 

!ENDPROTO:REM 

----Проверка 11. Начало.----
!IF $DEBUG# = 1,THEN 
!PROMPT 
0 Проверка 11
1 Сколько всего рудных тел было обработано макросом>'$t#'
!IF $t# <> $times#,GOTO ERR
!ECHO Проверка 11 пройдена успешно!
!ENDIF 
----Проверка 11. Конец.----

!SCRON 

!GOTO SUCSESS 

!ERR:ECHO !!!OOPS!!! ERROR!!!

!VARSAVE debug.var

!GOTO FIN

!SUCSESS:ECHO Work finished succsessfully

!FIN:REM 

!END 