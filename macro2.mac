!START OREHOL

-- Макрос создания рудной выборки
-- Автор - Полуэктов Полуэкт Полуэктович (polu@ekt.ru)
-- Версия от 01.01.1900

-- Имя исходного файла скважин
!LET $hol_name# = holes

-- Имя рудного каркаса
!LET $wf_name# = ore

-- Поле зонального контроля
!LET $zone_field# = BODY

-- Длина композитирования
!LET $comp_len# = 1

-- Имя урезаемого поля содержаний
!LET $metall_name# = AU

-- Уровень урезки
!LET $topcut# = 100

-- Имя финального файла рудной выборки
!LET $fin_ore_hol# = holes_ore_cut

==================================================
Внимание!!!! Настройки закончены!!! Ниже ничего менять нельзя!!!!
==================================================

!SELWF    &IN($hol_name#),
          &WIRETR($wf_name#_tr),
          &WIREPT($wf_name#_pt),
          &OUT(hol_sel),
          *X(X),
          *Y(Y),
          *Z(Z),
          *ZONE($zone_field#),
          @SELECT=3.0,
          @PLANE=-,
          @EXCLUDE=0.0,
          @TOLERANC=0.0,
          @CHECKROT=1.0,
          @ALLPTS=0.0

!SORTX    &IN(hol_sel),
          &OUT(hol_sel1),
          *KEY1(BHID),
          *KEY2(FROM),
          @ORDER=1.0,
          @KEYSFRST=1.0,
          @ROWORDER=1.0,
          @KEYTOL=0.00001

!COMPDH   &IN(hol_sel1),
          &OUT(hol_sel_c),
          *BHID(BHID),
          *FROM(FROM),
          *TO(TO),
          *ZONE(BODY),
          @INTERVAL=$comp_len#,
          @MINGAP=0.01,
          @MAXGAP=0.02,
          @MINCOMP=0.1,
          @START=0.0,
          @MODE=1.0

!EXTRA    &IN(hol_sel_c),
          &OUT($fin_ore_hol#),
          @APPROX=0.0
  
 $metall_name#_UR = $metall_name#
 if ($metall_name# > $topcut#)
  $metall_name#_UR = $topcut#
 end

GO 

!END 
