!START CHTREND

!ONERR GOTO ERR 

!SCROFF 

!LET $hol_in# = holes
!LET $wrk_field# = AU
!LET $cog_start# = 0.5
!LET $cog_end# = 3
!LET $cog_step# = 0.5
!LET $m_ore_start# = 1
!LET $m_ore_end# = 10
!LET $m_ore_step# = 1
!LET $var_source# = 1
1 – используются переменные, определённые выше
2 – используются переменные из файла переменных

-- Имя файла переменных
!LET $var_file# = chtrend.var

!LET $ou_file# = au_stat

==================================================

!IF $var_source# = 2,THEN 

 !VARLOAD $var_file#

!ENDIF 

!LET $current_cog# = $cog_start#

!EXTRA    &IN($hol_in#),
          &OUT(hol_mac_tmp),
          @APPROX=0.0
 
 $wrk_field#_=$wrk_field#
 saveonly(BHID,FROM,TO,LENGTH,X,Y,Z,A0,B0,$wrk_field#,$wrk_field#_)

GO 

!SORTX    &IN(hol_mac_tmp),
          &OUT(hol_mac_wrk),
          *KEY1(BHID),
          *KEY2(FROM),
          @ORDER=1.0,
          @KEYSFRST=1.0,
          @ROWORDER=1.0,
          @KEYTOL=0.00001

!DELETE   &IN(hol_mac_tmp),
          @CONFIRM=0.0

!LET $hol_sub_in# = hol_mac_wrk
!LET $field_to_comp# = $wrk_field#_
!LET $field_to_stat# = $wrk_field#
!LET $maxwaste_sub# = 3
!LET $file_ou_sub# = $ou_file#

!ECHO Подготовка завершена, начинаю основной расчёт

!EVERYSTEP:REM 

 !LET $current_m_ore# = $m_ore_start#

 !MORESTART:REM 

  !LET $cog_sub# = $current_cog#
  !LET $minore_sub# = $current_m_ore#
  !LET $need_append# = 0
  !FILE $file_name# = $ou_file#, $rec_count# = recs

  !IF $rec_count# > 0,THEN 

   !LET $need_append# = 1

  !ENDIF 

  !ECHO Начинаю расчёты для борта $current_cog# и длины $current_m_ore#

  !GOSUB STARTCOMP

  !ECHO Расчёты для борта $current_cog# и длины $current_m_ore# выполнены

  !LET $current_m_ore# = $current_m_ore# + $m_ore_step#
 !IF $current_m_ore# <= $m_ore_end#,GOTO MORESTART
 !LET $current_cog# = $current_cog# + 0.5

!IF $current_cog# <= $cog_end#,GOTO EVERYSTEP

=========== Подпрограмма композитирования и расчёта статистики =============

!GOTO ENDCOMP

!STARTCOMP:REM 

--Переменные:

$hol_sub_in# - файл скважин (сортирован по BHID/FROM)
$field_to_comp# - поле для композирования
$field_to_stat# - поле для расчёта статистики
$cog_sub# - борт
$minore_sub# - значение MINORE
$maxwaste_sub# - значение MAXWASTE
$file_ou_sub# - выходной файл статистики
$need_append# - признак необходимости присоединять результаты к выходному файлу 
                (0 - перезаписывать, 1 - присоединять)

!COMPSE   &IN($hol_sub_in#),
          &OUT(hol_sub_tmp),
          &HOLESOUT(hol_sub_tmp_),
          *VALUE($field_to_comp#),
          *OWCODE(RN),
          @CUTOFF=$cog_sub#,
          @MINORE=$minore_sub#,
          @MAXWASTE=$maxwaste_sub#,
          @MINASFR=0.95,
          @DILUTE=0.0,
          @NARWASTE=2.0,
          @ANYORE2=0.0

!ECHO Выделены кондиционные интервалы

!DELETE   &IN(hol_sub_tmp),
          @CONFIRM=0.0

!STATS    &IN(hol_sub_tmp_),
          &OUT(stat_sub_tmp),
          *F1($field_to_stat#),
          @KEYSORT=1.0,
          @KEYTOL=0.00001,
          @PCNTILES=1.0,
          @SORTOUT=1.0,RN=1

!DELETE   &IN(hol_sub_tmp_),
          @CONFIRM=0.0

!LET $sub_ou# = $file_ou_sub#

!IF $need_append# = 1,THEN 

 !LET $sub_ou# = stat_sub_tmp1

!ENDIF 

!EXTRA    &IN(stat_sub_tmp),
          &OUT($sub_ou#),
          @APPROX=0.0

 COG = $cog_sub#
 MINORE = $minore_sub#
 MAXWASTE = $maxwaste_sub#

GO 

!IF $need_append#=1,THEN 

 !APPEND   &IN1(stat_sub_tmp1),
           &IN2($file_ou_sub#),
           &OUT($file_ou_sub#_),
           @SEQUENCE=0.0,
           @PROTODD=0.0

 !DELETE   &IN(stat_sub_tmp1),
           @CONFIRM=0.0

 !COPY     &IN($file_ou_sub#_),
           &OUT($file_ou_sub#)

 !DELETE   &IN($file_ou_sub#_),
           @CONFIRM=0.0

!ENDIF 

!ECHO Выходной файл дополнен информацией по текущим кондициям

!RETURN 

!ENDCOMP:REM 

!SCRON 

!GOTO SUCSESS 

!ERR:ECHO !!!!!!!!!!!!!!!!!!!OOPS!!!ERROR!!!!!!!!!!!!!!!!!!!

!GOTO FIN

!SUCSESS:ECHO Work finished successfully

!FIN:REM 

!END 